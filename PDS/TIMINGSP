TIMINGS  TITLE 'Test execution speed of various instructions.'
*
* Rick Hevener, 3615 Devereaux Rd., Columbia, S.C. 29205;
* ph. 803-254-2083; e-mail rick.hevener@sc.edu .
*
* Program written in December, 1996.
*
* October, 1999:  for 9672-R55 and RC6 put several instr. inside each
* loop, align each loop on DWD; employ LOOP macro (sugg. by Jim
* Melnyk); add SQDR, SQER; make numerous cosmetic changes; file 291 on
* CBT tape (e-mail to and from Sam Golob).
*
* 2001-02-16:  add inline GOBK, HEXO, INIT macros; correct test section
* for displaying results of CPUTIMER; make a few cosmetic changes.
*
* 2002-07-01:  change NCY from 5000 to 25000 for Z800.
*
* 2002-08-24:  prevent 0C9 by shifting 12 places before division,
* instead of 9 before and 3 after (sugg. by Charlie Hottel).
*
* 2005-05-01:  modify some comments; revise LOOP macro; NOGEN it; add
* NOPR.
*
         SPACE 2
         PRINT OFF
         SPACE 1
         MACRO
&NAME    GOBK  &RC=0,&SR1=N
&NAME    L     R13,4(,R13)         Old save area address
         AIF   ('&SR1' NE 'Y').CKRC
         ST    R1,24(,R13)         Store R1 in old save area
.CKRC    AIF   ('&RC'(1,1) NE '(').NOTREG
         ST    &RC(1),16(,R13)     Store return code in old save area
         LM    R14,R12,12(R13)     Restore registers
         AGO   .RETURN
.NOTREG  LM    R14,R12,12(R13)     Restore registers
         LA    R15,&RC             Load return code
.RETURN  BR    R14                 Return
         MEND
         SPACE 1
         MACRO
&NAME    HEXO  &REG,&ADDR
&NAME    ST    R1,24(,R13)
         ST    &REG,16(,R13)
         LA    R1,&ADDR
         UNPK  15(9,R13),16(5,R13)
         TR    15(8,R13),D&SYSNDX-240
         MVC   0(8,R1),15(R13)
         L     R1,24(,R13)
         B     D&SYSNDX+16
D&SYSNDX DC    C'0123456789ABCDEF'
         MEND
         SPACE 1
         MACRO
&NAME    INIT  &L
&ID      SETC  '&SYSECT'
         AIF   ('&NAME' EQ '' AND '&L' NE 'PLI').STD
&ID      SETC  '&NAME'
         ENTRY &NAME               Establish entry point
         AIF   ('&L' NE 'PLI').STDE
         DS    0D                  Align so save area is on DWORD
         DC    CL9'&SYSDATC',CL6'&SYSTIME',CL8'&ID',X'8' Eye catcher
&NAME    STM   R14,R11,12(R13)     Store registers in old save area
         BAS   R15,88(,R15)        New save area address
         USING *,R13               Tell assembler about future base
         DC    20F'0'              Save area
         AGO   .LINK
.STDE    DS    0D                  Align so save area is on DWORD
.STD     ANOP
&NAME    B     104(,R15)           Branch to STM
         DC    X'8',CL9'&ID',CL9'&SYSDATC',CL9'&SYSTIME' Eye catcher
         USING *,R13               Tell assembler about future base
         DC    18F'0'              Save area
         STM   R14,R12,12(R13)     Store registers in old save area
         LA    R15,32(,R15)        New save area address
.LINK    ST    R15,8(,R13)         Forward linkage (new in old)
         ST    R13,4(,R15)         Backward linkage (old in new)
         LR    R13,R15             Est. base register as new SA addr.
         MEND
         SPACE 1
         MACRO
&N       LOOP  &I,&OPA
.* If no label is supplied when this macro is invoked, the instruction
.* name (&I) will be used as the branch label for the loop.
&L       SETC  '&I'
         AIF   ('&N' EQ '').A
&L       SETC  '&N'
.A       MVC   L0108,=CL08'&L'
         CNOP  4,8                 Align loop below on double word
         BAS   R11,CPUTIMER        Time before loop
         SPACE 1
&L       DS    0H
         AIF   (K'&OPA LE 2).C
&OPB     SETC  '&OPA'(2,K'&OPA-2)
&C       SETA  NINST
.B       &I    &OPB
&C       SETA  &C-1
         AIF   (&C GT 0).B
.C       BCT   R4,&L
         BAS   R10,PSTLOOP         Post-loop processing
         MEND
         SPACE 1
         MACRO
&NAME    REGEQU &FL=N
&I       SETA  0
.L       ANOP
R&I      EQU   &I
         AIF   ('&FL' NE 'Y' OR &I GT 6 OR &I NE &I/2*2).C
F&I      EQU   &I
.C       ANOP
&I       SETA  &I+1
         AIF   (&I LT 16).L
         MEND
         PRINT ON,NOGEN
         SPACE 3
TIMINGS  CSECT
TIMINGS  AMODE 24
TIMINGS  RMODE 24
         SPACE 1
         REGEQU FL=Y
         PRINT GEN
NCY      EQU   025000              NUMBER OF CYCLES PER LOOP
NINST    EQU   11                  NUMBER OF INSTR. PER CYCLE
         SPACE 1
         USING *+32+4096,R12       Get ready for 2nd base reg.
         INIT  ,
         SPACE 1
         LA    R12,2048(,R13)      Set up 2nd base reg.
         LA    R12,2048(,R12)
         SPACE 1
* Initialize registers that will not be changed.
         L     R5,NCYA             NUMBER OF CYCLES PER LOOP
         LA    R6,1
         LD    F4,ONE
         LD    F6,ONE+8
         SPACE 1
         OPEN  (PRINT,OUTPUT)
         SPACE 1
         MVC   L00,SPACES          CLEAR LINE
         MVC   L0041,=CL41'1TIMINGS Version 2.08'
         BAS   R11,PUTPRINT        WRITE HEADER
         SPACE 1
         MVC   L0041,=CL41'0Number of cycles per loop:'
         LR    R9,R5               NUMBER OF CYCLES PER LOOP
         BAS   R10,PSTLOOPD        WRITE OUT THE NUMBER
         SPACE 1
         MVC   L0041,=CL41'0Number of instructions per cycle:'
         LA    R9,NINST            NUMBER OF INSTR. PER CYCLE
         BAS   R10,PSTLOOPD        WRITE OUT THE NUMBER
*                                  AND SET THINGS UP FOR FIRST LOOP
         SPACE 1
         MVC   L00,SPACES          CLEAR LINE
         BAS   R11,PUTPRINT        WRITE A BLANK LINE
         SPACE 1
         MVC   L1012,=CL12'picoseconds:' SET UP FOR LOOPS
         SPACE 1
         STIMER TASK,BINTVL=LONGTIME
         SPACE 1
         PRINT NOGEN
OVERHEAD LOOP  ,                   OBTAIN CYCLE OVERHEAD TIME
* The cycle overhead time will be subtracted from the time per cycle
* for each of the loops below.
         LOOP  AR,(R2,R6)
         LOOP  ALR,(R2,R6)
*         B     DONE
         LOOP  AXR,(F0,F4)
         LOOP  ADR,(F0,F4)
         LOOP  AER,(F0,F4)
         LOOP  AWR,(F0,F4)
         LOOP  AUR,(F0,F4)
         LOOP  NR,(R2,R6)
         LOOP  BALR,(R2,R0)
         LOOP  BASR,(R2,R0)
         LOOP  BASSM,(R2,R0)
         LOOP  BSM,(R2,R0)
         LOOP  BR,(R0)
         LOOP  BNOR,(R0)
         LOOP  NOPR,(R0)
         LOOP  NOP,(0)
         LOOP  BCTR,(R2,R0)
         LOOP  CR,(R2,R6)
         LOOP  CDR,(F0,F4)
         LOOP  CER,(F0,F4)
         LOOP  CLR,(R2,R6)
         LOOP  DR,(R2,R6)
         LOOP  DXR,(F0,F4)
         LOOP  DDR,(F0,F4)
         LOOP  DER,(F0,F4)
         LOOP  XR,(R2,R6)
         LOOP  HDR,(F0,F4)
         LOOP  HER,(F0,F4)
         LOOP  IPM,(R2)
         LOOP  LR,(R2,R6)
         LOOP  LDR,(F0,F4)
         LOOP  LER,(F0,F4)
         LOOP  LA,(R2,4095(R6,R2))
         LOOP  LTR,(R2,R6)
         LOOP  LTDR,(F0,F4)
         LOOP  LTER,(F0,F4)
         LOOP  LCR,(R2,R6)
         LOOP  LCDR,(F0,F4)
         LOOP  LCER,(F0,F4)
         LOOP  LNR,(R2,R6)
         LOOP  LNDR,(F0,F4)
         LOOP  LNER,(F0,F4)
         LOOP  LPR,(R2,R6)
         LOOP  LPDR,(F0,F4)
         LOOP  LPER,(F0,F4)
         LOOP  LRDR,(F0,F4)
         LOOP  LRER,(F0,F4)
         LOOP  MR,(R2,R6)
         LOOP  MXR,(F0,F4)
         LOOP  MXDR,(F0,F4)
         LOOP  MDR,(F0,F4)
         LOOP  MER,(F0,F4)
         LOOP  OR,(R2,R6)
         LOOP  SPM,(R2)
         LOOP  SLDA,(R2,63)
         LOOP  SLDL,(R2,63)
         LOOP  SLA,(R2,63)
         LOOP  SLL,(R2,63)
         LOOP  SRDA,(R2,63)
         LOOP  SRDL,(R2,63)
         LOOP  SRA,(R2,63)
         LOOP  SRL,(R2,63)
         LOOP  SQDR,(F0,F4)
         LOOP  SQER,(F0,F4)
         LOOP  SR,(R2,R6)
         LOOP  SLR,(R2,R6)
         LOOP  SXR,(F0,F4)
         LOOP  SDR,(F0,F4)
         LOOP  SER,(F0,F4)
         LOOP  SWR,(F0,F4)
         LOOP  SUR,(F0,F4)
         PRINT GEN
         SPACE 1
*DONE     DS    0H
         MVC   L00,SPACES          CLEAR LINE
         MVC   L0041,=CL41'0**** Normal program end'
         BAS   R11,PUTPRINT
         SPACE 1
         CLOSE (PRINT)
         SPACE 1
         GOBK  ,
         SPACE 2
CPUTIMER CPUTIMER MIC,TM           BIT 51 = 1 MICROSEC.
         BR    R11
         SPACE 2
PSTLOOP  LM    R8,R9,TM            SAVE TIME BEFORE LOOP
         BAS   R11,CPUTIMER        TIME AFTER LOOP
         SPACE 1
* Begin test section
*         MVC   L00SV,L00           Save line
*         MVC   L00,SPACES          Clear line
*         BAS   R7,HEXO             Display time before loop
*         STM   R8,R9,TMH           Save time before loop
*         LM    R8,R9,TM            Time after loop
*         BAS   R7,HEXO             Display time after loop
*         LM    R8,R9,TMH           Restore time before loop
*         MVC   L00,L00SV           Restore line
*         B     HEXOA
*HEXO     DS    0H
*         HEXO  R8,L00+01
*         HEXO  R9,L00+10
*         BAS   R11,PUTPRINT
*         BR    R7
*TMH      DS    2F
*L00SV    DS    CL121
*HEXOA    DS    0H
* End test section
         SPACE 1
* COMPUTE ELAPSED TIME.
         SL    R9,TM+4
         BC    3,PSTLOOPA
         SPACE 1
         BCTR  R8,0                BORROW
         SPACE 1
PSTLOOPA SL    R8,TM
         SPACE 1
* Make R8-R9 < 2**31 for multiplication below.
* Set up for next loop.
         LNR   R2,R6               # of net right shifts = -1
* Perform left shift of 2; one will be restored inside next loop,
* other one just after it.
         SLDL  R8,2
         SPACE 1
PSTLOOPB AR    R2,R6               # = # + 1
         SRDL  R8,1
         LTR   R8,R8               R8-R9 < 2**32?
         BNZ   PSTLOOPB
         SPACE 1
         SRDL  R8,1                Now R8-R9 < 2**31
         SPACE 1
         M     R8,=F'1E6'          CONVERT MICROSEC. TO PICOSEC.
* RESTORE SHIFTS, MAKING BIT 51 = 1 PICOSEC.
         SLDL  R8,0(R2)
* Make bit 63 = 1 picosec.; do this before division to prevent 0C9.
         SRDL  R8,12
         DR    R8,R5               ELAPSED TIME / CYCLES
         SPACE 1
         CLC   L0108,=CL08'OVERHEAD' DOING OVERHEAD LOOP?
         BNE   PSTLOOPC
         ST    R9,TMO              SAVE CYCLE OVERHEAD TIME
         B     PSTLOOPD
         SPACE 1
PSTLOOPC DS    0H
         S     R9,TMO              SUBTRACT CYCLE OVERHEAD TIME
         LA    R2,NINST            NUMBER OF INSTR. PER CYCLE
         LR    R8,R9               GET READY FOR DIVISION
         SRDA  R8,32
         DR    R8,R2               TIME PER INSTR.
         SPACE 1
* WRITE OUT THE NUMBER.
PSTLOOPD DS    0H
         CVD   R9,WORK
         MVC   L41LP,PTRN
         LA    R1,L41LP+LPTRN-1
         EDMK  L41LP,WORK+2
         BNM   PSTLOOPE
         SPACE 1
         BCTR  R1,0
         MVI   0(R1),C'-'
         SPACE 1
PSTLOOPE DS    0H
         BAS   R11,PUTPRINT
* Set things up for next loop.
         LR    R4,R5               NUMBER OF CYCLES PER LOOP
         SR    R2,R2               SET UP REGS FOR COMPUTATIONS
         LR    R3,R6
         LDR   F0,F4
         LDR   F2,F6
         BR    R10
         SPACE 2
PUTPRINT PUT   PRINT,L00
         BR    R11
         DROP  R13,R12
         SPACE 3
         LTORG
         SPACE 1
         PRINT NOGEN
PRINT    DCB   MACRF=PM,DSORG=PS,DDNAME=SYSPRINT,                      X
               RECFM=FBA,LRECL=121
         SPACE 1
ONE      DC    L'1'
LONGTIME DC    F'2E9'
NCYA     DC    A(NCY)              NUMBER OF CYCLES PER LOOP
* Pattern for editing integers follows:
PTRN     DC    X'404020206B2020206B2020206B202120'
LPTRN    EQU   *-PTRN
SPACES   DC    CL121' '
         SPACE 1
TM       DS    D
WORK     DS    D
TMO      DS    F
L00      DS    CL121
L0041    EQU   L00+00,41
L0108    EQU   L00+01,08
L1012    EQU   L00+10,12
L41LP    EQU   L00+41,LPTRN
         SPACE 1
         END   TIMINGS
